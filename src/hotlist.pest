HOTLIST = {
  SOI ~ HOTLIST_HEADER ~ HOTLIST_BODY ~ EOI
}

HOTLIST_HEADER = ${
  "Opera Hotlist version " ~ HOTLIST_VERSION ~ "\n" ~ HOTLIST_OPTIONS
}

HOTLIST_VERSION = {
  ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+
}

HOTLIST_OPTIONS = {
	"Options: " ~ HOTLIST_ENCODING
}

HOTLIST_ENCODING = {
    "encoding = " ~ UTF8_ENCODING
}

UTF8_ENCODING = {
	"utf8, version=" ~ UTF8_VERSION
}

UTF8_VERSION = {
  ASCII_DIGIT+
}

HOTLIST_BODY = {
	(FOLDER | NOTE)+
}

WHITESPACE = _{
	" " | "\t" | NEWLINE
}

NOTE = {
	"#NOTE" ~ ID ~ UNIQUEID ~ NAME? ~ URL? ~ CREATED ~ ACTIVE?
}

FOLDER_START = {
	"#FOLDER" ~ ID ~ NAME ~ CREATED ~ (EXPANDED | TRASH) ~ UNIQUEID
}

FOLDER_END = _{
	"-"
}

FOLDER = {
	FOLDER_START ~ (NOTE)* ~ FOLDER_END
}

ID = _{
	"ID" ~ "=" ~ HLID
}

HLID = {
	ASCII_DIGIT+
}

UNIQUEID = _{
	"UNIQUEID" ~ "=" ~ UUID
}

UUID = ${
	ASCII_HEX_DIGIT+
}

NAME = ${
	"NAME=" ~ (!NEWLINE ~ (INBAND_NEWLINE | NAME_TEXT))+
}

NAME_TEXT = {
	(!(INBAND_NEWLINE | NEWLINE) ~ ANY)+
}

INBAND_NEWLINE = {
    "\u{02}\u{02}"
}

URL = _{
	"URL" ~ "=" ~ LINK
}

// Newlines and whitespace are invalid in an http link.
LINK = @{
	(ASCII_ALPHANUMERIC | ":" | "/" | "." | "-" | "~" |
    "_" | "#" | "$" | "," | ";" | "(" | ")" | "'" | "?" |
    "[" | "]" | "@" | "!" | "&" | "*" | "+" | "=" |
    PERCENT_ENCODING | HACK_CHARS)+
}

PERCENT_ENCODING = {
	"%" ~ ASCII_HEX_DIGIT{2}
}

// Occasionnally I find invalid URI chars... just put them here for now.
HACK_CHARS = {
	"\u{2019}"
}

CREATED = _{
	"CREATED" ~ "=" ~ TIME
}

TIME = {
	ASCII_DIGIT+
}

ACTIVE = _{
	"ACTIVE=" ~ ("YES" | "NO")
}

EXPANDED = _{
	"EXPANDED=" ~ ("YES" | "NO")
}

TRASH = _{
	"TRASH FOLDER=" ~ ("YES" | "NO")
}
